apply plugin: 'com.android.library'
apply plugin: 'project-report'

android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 30
        versionName "3.0.0"
        project.archivesBaseName = "AndroidMVC"
        project.version = android.defaultConfig.versionName

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main{
            res.srcDirs += 'src/main/res-ui'
            res.srcDirs += 'src/main/res-dynamic'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
        quiet true
    }
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-${version}.aar"
            }
        }
    }
}
task generateSourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task generateJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
generateJavadocs.dependsOn generateSourcesJar

task generateJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from generateJavadocs.destinationDir
}
generateJavadocsJar.dependsOn generateJavadocs

dependencies {
    // A N D R O I D    S U P P O R T
    implementation 'com.android.support:support-core-ui:27.0.1'
    implementation 'com.android.support:support-v13:27.0.1'
    implementation 'com.android.support:recyclerview-v7:27.0.1'

    // P R O J E C T    D E P E N D E N C I E S

    // L O C A L    L I B R A R I E S
    implementation files('libs/CoreBase.core-2.0.2.jar')
    api files('libs/viewpagerindicator-release.aar')

    // M O D U L E    D E P E N D E N C I E S
    implementation 'org.slf4j:slf4j-android:1.7.25'
    implementation 'joda-time:joda-time:2.3'
    implementation 'org.apache.commons:commons-lang3:3.7'

    // P L A T F O R M    D E P E N D E N C I E S

    // D E P E N D E N C I E S    F R O M    M O D U L E S

    // T E S T I N G
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    //SQLite -> Chrome (debug)
    implementation 'com.facebook.stetho:stetho:1.5.0'
    implementation 'com.facebook.stetho:stetho-urlconnection:1.5.0'
}
